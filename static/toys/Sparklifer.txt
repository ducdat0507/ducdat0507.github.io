Let's make a game!
	name:Sparklifer!
	desc:<q>A game about making universes...</q>
Settings
Layout
	*main
		contains:log, res, buttons
		*log
			contains:Log
		*res
			contains:Resources
			class:fullWidth
		*buttons
			contains:Buttons
	*panel
		contains:tagHolder, titleHolder, menu
		*tagHolder
			contains:tag:menuButtons
		*titleHolder
			contains:tag:title
		*menu
 			contains:tag:statOverall, tag:achieves, tag:config, tag:sparkGens, tag:taskStory, tag:taskUpgrades, tag:colonCapBox, tag:colonHabtBox, tag:ascension, tag:artifacts, tag:antishop
			costs:hide
CSS
	#game
	{
		background:#000;
		font-size:14px;
		color:rgba(0,0,0,0.9);
		text-shadow:none;
		font-family:"Trebuchet MS", Helvetica, sans-serif
	}
	#box-panel
	{
		right:0px;
		top:0px;
		bottom:0px;
		width:40%;
		position:absolute;
		padding-bottom:0px;
	}
	#box-log
	{
		left:0px;
		right:0%;
		top:50px;
		bottom:0px;
		position:absolute;
		overflow-y:hidden;
	}
	#box-tagHolder
	{
		right:0px;
		top:0px;
		height:75px;
		left:0px;
		overflow-y:hidden;
		position:absolute;
		align-self:flex-start;
		display:flex;
		align-items:flex-end;
		transition:all 0.25s ease;
	}
	#box-tagHolder:hover
	{
		height:60px;
	}
	#box-titleHolder
	{
		right:0px;
		top:50px;
		height:28px;
		left:0px;
		overflow-y:hidden;
		position:absolute;
	}
	.alignBottom
	{
		vertical-align:bottom;
		bottom:0;
		left:0;
	}
	#box-menu
	{
		right:0px;
		bottom:48px;
		top:78px;
		left:0px;
		position:absolute;
		overflow-y:scroll;
	}
	.fullWidth,.fullWidth:hover
	{
		background:rgba(0,0,0,1);
		box-shadow:none;
		color:#fff;
	}
	.thing.cantAfford
	{
		background:rgba(255, 127, 127, 0.5);
		opacity:0.65;
	}
	.title
	{
		text-decoration:underline;
		font-size:18px;
	}
	.generatorClass
	{
		transition:all 0.2s ease;
	}
	.generatorClass:hover
	{
		background:rgba(255,255,255,0.75);
		padding:5px;
	}
	.achievClass
	{
		transition:all 0.2s ease;
		text-align:left;
		border:1px solid rgba(0,0,0,0.9);
	}
	.achievClass:hover
	{
		background:rgba(255,255,255,0.75);
		padding:5px;
		padding-left:10px;
	}
	.roundOnHover 
	{
		transition:border-radius 0.5s ease;
	}
	.roundOnHover:hover
	{
		border-radius:8px;
	}
	.panelTab
	{
		border:1px solid rgba(255,255,255,0.9);
		margin:1px;
		color:#00000000;
		background:rgba(0,0,0,1);
		flex:1;
		flex-direction:row;
		width:25px;
		height:30px;
		transition:all 0.35s ease;
	}
	.panelTab:hover
	{
		border:1px solid rgba(255,255,255,0.9);
		border-right:1px solid rgba(127,127,127,0.9);
		background:rgba(15,15,15,1);
		color:#FFFFFFFF;
		height:60px;
	}
	.ascendClass.dim
	{
		filter:blur(5px);
	}
	.textOnly
	{
		background:#00000000;
		border:none;
		color:#fff;
		width:99%;
	}
	.textOnly:hover
	{
		background:#00000000;
		border:none;
		color:#fff;
		width:99%;
		cursor:default;
	}
	.alignLeft
	{
		text-align:left;
	}
Resources
	*spark|sparks
		name:Spark|Sparks
		text:<t>[this]</t>spark[s?this]<q>[realSparkPS][?realSparkPS<1000|.[realSparkPS*10%10]][?realSparkPS<100|[realSparkPS*100%10]] per second</q>
		desc:The basic resource of the universe. Use this to purchase upgrades and stuff like that.
		class:fullWidth
	*TEMPLATE
		hidden
	*sparkThisVerse
	*cps
	*realSparkPS
		on tick:realSparkPS is spark:ps+cps
		on tick:lose realSparkPS-spark:ps cps
	*prestigeTimes
		tag:permanent
	*fragment|fragments
		tag:permanent
	*antimatter
		tag:permanent
	*panelPage
		can be negative
		on load:panelPage is -1
		tag:permanent
	*clickEff
		on tick:yield 1 clickEff
	*ascendValue
		is always:autoclicker/15+sparker/14+spotlight/13+sun/12+galaxy/11+upgradeCount:ps/5
	*ascendValueMax
		is always:max(ascendValueMax, ascendValue)
		tag:permanent
	*TEMPLATE
		hidden
		tag:effs
	*autoclickerEff
		on tick:yield 0.1 autoclickerEff
	*sparkerEff
		on tick:yield 0.5 sparkerEff
	*spotlightEff
		on tick:yield 3 spotlightEff
	*sunEff
		on tick:yield 18 sunEff
	*galaxyEff
		on tick:yield 108 galaxyEff
	*TEMPLATE
		hidden
	*colonCap
	*habitability
	*upgradeCount
	*playtime
		tag:permanent
		on tick:yield 1 playtime
	*playtimeLastOpen
		tag:permanent
		on tick:yield 1 playtimeLastOpen
		on load:playtimeLastOpen is 0
	*playtimeThisVerse
		on tick:yield 1 playtimeThisVerse
Buttons
	*sparklifer
		name:The Sparklifer
		no text
		desc:Click this to get some sparks.
		class:bigButton hasFlares
		on click:
			yield clickEff:ps sparks
			yield clickEff:ps cps
			yield clickEff:ps sparkThisVerse
			if (chance(0.2%))
				yield 1 antimatter
				toast You've got 1 antimatter!
				log You've got 1 antimatter!
			end
		end
		on click:spawn ClickPar
		on save:log Game saved
	*title
		text:<t>[?panelPage=-2|Statistics][?panelPage=-1|Welcome to Sparklifer!][?panelPage=0|Configurations][?panelPage=1|Generators][?panelPage=2|Tasks][?panelPage=3|Colonization][?panelPage=4|Prestige][?panelPage=5|Artifact][?panelPage=6|Antishop]</t>
		no tooltip
		tag:title
		class:alignBottom fullWidth
	*TEMPLATE
		class:panelTab alignBottom
		no tooltip
	*panelStats
		name:Statistics
		tag:menuButtons
		on click:panelPage is -2
		on tick:
			if (panelPage=-2)
				show tag:statOverall
				show tag:achieves
			else 
				hide tag:statOverall
				hide tag:achieves
			end
		end
	*panelConfig
		name:Configurations
		tag:menuButtons
		on click:panelPage is 0
		on tick:
			if (panelPage=0)
				show tag:config
			else 
				hide tag:config
			end
		end
	*panelGenerators
		name:Generators
		tag:menuButtons
		on click:panelPage is 1
		on tick:
			if (panelPage=1)
				show tag:sparkGens
			else 
				hide tag:sparkGens
			end
		end
	*panelTasks
		name:Tasks
		tag:menuButtons
		on click:panelPage is 2
		on tick:
			if (panelPage=2)
				show tag:taskStory
				show tag:taskUpgrades
			else 
				hide tag:taskStory
				hide tag:taskUpgrades
			end
		end
	*panelColon
		name:Colonization
		tag:menuButtons
		on click:panelPage is 3
		on tick:
			if (panelPage=3)
				show tag:colonCapBox
			else 
				hide tag:colonCapBox
			end
			if (story6)
				show this
			else
				hide this
			end
		end
	*panelPrestige
		name:Prestige
		tag:menuButtons
		on click:panelPage is 4
		on tick:
			if (panelPage=4)
				show tag:ascension
			else 
				hide tag:ascension
			end
			if (story7 + prestigeTimes)
				show this
			else
				hide this
			end
		end
	*panelArtifacts
		name:Artifacts
		tag:menuButtons
		on click:panelPage is 5
		on tick:
			if (panelPage=5)
				show tag:artifacts
			else 
				hide tag:artifacts
			end
			if (ItemsLeft<100)
				show this
			else
				hide this
			end
		end
	*panelAntishop
		name:Antishop
		tag:menuButtons
		on click:panelPage is 6
		on tick:
			if (panelPage=6)
				show tag:antishop
			else 
				hide tag:antishop
			end
			if (antimatter:earned)
				show this
			else
				hide this
			end
		end
	*TEMPLATE
		class:fullWidth
		no tooltip
	*boxOverall
		name:Overall
		tag:statOverall
	*boxAchieves
		name:Achievements
		tag:achieves
	*boxSparkGens
		name:Spark Generators
		tag:sparkGens
	*boxStory
		name:Story
		tag:taskStory
	*boxUpgrades
		text:Upgrades: [upgradeCount:ps]
		tag:taskUpgrades
		on tick:
			if (min(story3, panelPage=2))
				show this
			else
				hide this
			end
		end
	*boxColonProgress
		name:Colonization Capacity
		text:Colonization Capacity: [colonCap:ps]
		tag:colonCapBox
		on tick:
			if (panelPage=3)
				show this
			else
				hide this
			end
		end
	*boxAscension
		name:Ascension
		tag:ascension
	*TEMPLATE
		class:textOnly
	*boxFragments
		name:Fragment
		text:<t>[fragment]</t>fragment[s?fragment]<q>+[fragment:earned]% permanent boost<q>
		tag:ascension permanent
		desc:What you'll get from ascending. Use this to forge artifacts. 
		on tick:multiply yield of clickEff by 1+fragment:earned/100
		on tick:multiply yield of tag:effs by 1+fragment:earned/100
	*boxAntimatter
		name:Antimatter
		text:<t>[antimatter]</t>antimatter
		tag:antishop permanent
		desc:A rare kind of currency that can only be found through tons of clicking. Permanent upgrades need this.
	*boxItemLeft
		text:Artifact slot: [100-ItemsLeft] / 100
		tag:artifacts permanent
		no tooltip
	*TEMPLATE
	*overallStats
		text:<t>Sparks</t>Total made: <b>[spark:earned]</b><//>[?prestigeTimes>0|This universe made: <b>[sparkThisVerse]</b>]<//>Maximum reached: <b>[spark:max]</b><t>Clicking</t>Sparklifer clicks: <b>[sparklifer:clicks]</b><//>Spark per click: <b>[clickEff:ps]</b><t>Playtime</t>Total: <b>[?playtime/86400 >= 1|[playtime/86400] day[s?floor(playtime/86400)], ][?playtime/3600 >= 1|[playtime/3600%24] hour[s?floor(playtime/3600%24)], ][?playtime/60 >= 1|[playtime/60%60] minute[s?floor(playtime/60%60)] and ][playtime%60] second[s?playtime%60]</b><//>Last opened: <b>[?playtimeLastOpen/86400 >= 1|[playtimeLastOpen/86400] day[s?floor(playtimeLastOpen/86400)], ][?playtimeLastOpen/3600 >= 1|[playtimeLastOpen/3600%24] hour[s?floor(playtimeLastOpen/3600%24)], ][?playtimeLastOpen/60 >= 1|[playtimeLastOpen/60%60] minute[s?floor(playtimeLastOpen/60%60)] and ][playtimeLastOpen%60] second[s?playtimeLastOpen%60]</b><//>This universe: <b>[?playtimeThisVerse/86400 >= 1|[playtimeThisVerse/86400] day[s?floor(playtimeThisVerse/86400)], ][?playtimeThisVerse/3600 >= 1|[playtimeThisVerse/3600%24] hour[s?floor(playtimeThisVerse/3600%24)], ][?playtimeThisVerse/60 >= 1|[playtimeThisVerse/60%60] minute[s?floor(playtimeThisVerse/60%60)] and ][playtimeThisVerse%60] second[s?playtimeThisVerse%60]</b>[?prestigeTimes>0|<t>Prestige</t>Total ascension: <b>[prestigeTimes]</b><//>Most valuable universe: <b>+[ascendValueMax] fragments</b>]
		no tooltip
		tag:statOverall
		class:textOnly alignLeft
	*ascend
		name:Ascend!
		text:<t>Ascend!</t>+[ascendValue] fragment[s?ascendValue]
		tag:ascension
		desc:Destroy and rebuild your universe for fragments. You will lose everything except:<.>Fragments (of course!)<.>Antimatter<.>Artifacts<.>Permanent upgrades<//>[?story7|<#0f0>You can ascend now to get [ascendValue] fragments. It is recommended to buy everything you can before ascending.|<#f00>Your universe need to be at the rank 7 before you can ascend!]</#>
		class:building ascendClass dim
		on tick: 
			if (story7) 
				light this 
			else 
				dim this
			end
		end
		on click:
			if (story7)
				yield ascendValue fragment
				yield 1 prestigeTimes
				lose notTag:permanent
			end
		end
	*artifactBuy
		name:Ascend!
		text:Forge an artifact for 15 fragments
		tag:ascension
		no tooltip
		class:building dim
		on tick: 
			if (fragment>=15) 
				light this 
			else 
				dim this
			end
		end
		on click:
			if (fragment>=15)
				if (chance(75%))
					yield clickScroll1
				else if (chance(75%))
					yield clickScroll2
				else
					yield clickScroll3
				end
				lose 15 fragments
			end
		end
Buildings
	*TEMPLATE
		tooltip origin:left
		class:generatorClass
	*autoclicker|autoclickers
		name:Autoclicker|Autoclickers
		
		text:[this] autoclicker[s?this]<//><b>[?sparks>(15*pow(1.15, autoclicker))|<#0f0>|<#f00>][15*pow(1.15, autoclicker)] sparks </#></b>
		desc:Clicks out [autoclickerEff:ps][?autoclickerEff:ps<100|.[autoclickerEff:ps*10%10]] spark[s?autoclickerEff:ps] per second.<q>"A mouse cursor that clicks every ten seconds."
		cost:15 sparks
		tag:sparkGens
		on tick:yield autoclickerEff:ps sparks
		on tick:yield autoclickerEff:ps sparkThisVerse
	*sparker|sparkers
		name:Sparker|Sparkers
		text:[this] sparker[s?this]<//><b>[?sparks>(60*pow(1.15, sparker))|<#0f0>|<#f00>][60*pow(1.15, sparker)] sparks </#></b>
		desc:Produces [sparkerEff:ps][?sparkerEff:ps<100|.[sparkerEff:ps*10%10]] spark[s?sparkerEff:ps] per second.
		req:5 autoclickers, story1
		cost:60 sparks
		tag:sparkGens
		on tick:yield sparkerEff:ps sparks
		on tick:yield sparkerEff:ps sparkThisVerse
	*spotlight|spotlights
		name:Spotlight|Spotlights
		text:[this] spotlight[s?this]<//><b>[?sparks>(500*pow(1.15, spotlight))|<#0f0>|<#f00>][500*pow(1.15, spotlight)] sparks </#></b>
		desc:Brights out [spotlightEff:ps][?spotlightEff:ps<100|.[spotlightEff:ps*10%10]] spark[s?spotlightEff:ps] per second.
		req:10 sparkers, story3
		cost:500 sparks
		tag:sparkGens
		on tick:yield spotlightEff:ps sparks
		on tick:yield spotlightEff:ps sparkThisVerse
	*sun|suns
		name:Sun|Suns
		text:[this] sun[s?this]<//><b>[?sparks>(8200*pow(1.15, sun))|<#0f0>|<#f00>][8200*pow(1.15, sun)] sparks </#></b>
		desc:Shines out [sunEff:ps][?sunEff:ps<100|.[sunEff:ps*10%10]] spark[s?sunEff:ps] per second.
		req:15 spotlights, story5
		cost:8200 sparks
		tag:sparkGens
		on tick:yield sunEff:ps sparks
		on tick:yield sunEff:ps sparkThisVerse
	*galaxy|galaxies
		name:Galaxy|Galaxies
		text:[this] galax[?galaxy=1|y|ies]<//><b>[?sparks>(268420*pow(1.15, galaxy))|<#0f0>|<#f00>][268420*pow(1.15, galaxy)] sparks </#></b>
		desc:Brights out [galaxyEff:ps][?galaxyEff:ps<100|.[galaxyEff:ps*10%10]] spark[s?galaxyEff:ps] per second.
		req:20 suns, story7
		cost:268420 sparks
		tag:sparkGens
		on tick:yield galaxyEff:ps sparks
		on tick:yield galaxyEff:ps sparkThisVerse
	*planet|planets
		name:Planet|Planets
		text:[this] planet[s?this]<//><b>[?sparks>200000|<#0f0>|<#f00>][200000] sparks </#></b>
		desc:+5 spark per second. You can only have 10 planet per 1 star owned
		cost:200000 sparks
		cost increase:100%
		limit:10*sun
		tag:colonCapBox
		on tick:yield 5 sparks
	*terresPlanet|terresPlanets
		name:Terrestrial planet|Terrestrial planets
		text:[this] terrestrial planet[s?this]<//><b>[?sparks>=1000000 and planets>=5|<#0f0>|<#f00>][1000000] sparks + 5 planets</#></b>
		desc:+25 spark per second. Increases colonization capacity by 10.
		cost:1000000 sparks, 5 planets
		req:planets>=5 or terresPlanet
		cost increase:100%
		limit:2*sun
		tag:colonCapBox
		on tick:yield 25 sparks
		on tick:yield 10 colonCap
	*habitPlanet|habitPlanets
		name:Habitable planet|Habitable planets
		text:[this] habitable planet[s?this]<//><b>[?sparks>=5000000 and terresPlanets>=5|<#0f0>|<#f00>][5000000] sparks + 5 terrestrial planets</#></b>
		desc:Increases colonization capacity by 200.
		cost:5000000 sparks, 5 terresPlanets
		req:terresPlanets>=5 or habitPlanet
		cost increase:100%
		limit:sun
		tag:colonCapBox
		on tick:yield 200 colonCap
Upgrades
	*story1
		name:The Big Bang
		text:Universe rank 0<t>The Big Bang</t>
		desc:<.>Universe <b>rank up!</b><.>Idle production <b>+1%</b><.>Sparklifer power <b>+1</b><.><b>New generators!</b>
		cost:250 sparks
		tag:taskStory
		class:building
		passive:hide this
		passive:multiply yield of tag:effs by 1.01
		passive:increase yield of clickEff by 1
	*story2
		name:Heatdown
		text:Universe rank 1<t>Heatdown</t>
		desc:<.>Universe <b>rank up!</b><.>Idle production <b>+1.5%</b><.>Sparklifer power <b>+2</b>
		req:story1
		cost:1000 sparks
		tag:taskStory
		class:building
		passive:hide this
		passive:multiply yield of tag:effs by 1.015
		passive:increase yield of clickEff by 2
	*story3
		name:Dustify
		text:Universe rank 2<t>Dustify</t>
		desc:<.>Universe <b>rank up!</b><.>Idle production <b>+2%</b><.>Sparklifer power <b>+3</b><.><b>Some neat upgrades!</b><.><b>New generator!</b>
		req:story2
		cost:5000 sparks
		tag:taskStory
		class:building
		passive:hide this
		passive:multiply yield of tag:effs by 1.02
		passive:increase yield of clickEff by 3
	*story4
		name:Create planets
		text:Universe rank 3<t>Create planets</t>
		desc:<.>Universe <b>rank up!</b><.>Idle production <b>+2.5%</b><.>Sparklifer power <b>+4</b><.><b>More neat upgrades!</b>
		req:story3
		cost:25000 sparks
		tag:taskStory
		class:building
		passive:hide this
		passive:multiply yield of tag:effs by 1.025
		passive:increase yield of clickEff by 4
	*story5
		name:Create stars
		text:Universe rank 4<t>Create stars</t>
		desc:<.>Universe <b>rank up!</b><.>Idle production <b>+3%</b><.>Sparklifer power <b>+5</b><.><b>Even more neat upgrades!</b><.><b>New generator!</b>
		req:story4
		cost:100000 sparks
		tag:taskStory
		class:building
		passive:hide this
		passive:multiply yield of tag:effs by 1.03
		passive:increase yield of clickEff by 5
	*story6
		name:Colonization
		text:Universe rank 5<t>Colonization</t>
		desc:<.>Universe <b>rank up!</b><.>Idle production <b>+3.5%</b><.>Sparklifer power <b>+6</b><.><b>Colonization!</b>
		req:story5
		cost:500000 sparks
		tag:taskStory
		class:building
		passive:hide this
		passive:multiply yield of tag:effs by 1.035
		passive:increase yield of clickEff by 6
	*story7
		name:Galaxium
		text:Universe rank 6<t>Galaxium</t>
		desc:<.>Universe <b>rank up!</b><.>Idle production <b>+4%</b><.>Sparklifer power <b>+7</b><.><b>Galaxies!</b><.>Unlocks <b>Ascension</b>
		req:story6
		cost:2500000 sparks
		tag:taskStory
		class:building
		passive:hide this
		passive:multiply yield of tag:effs by 1.035
		passive:increase yield of clickEff by 6
	*story8
		name:Researcher
		text:Universe rank 7<t>Researcher</t>
		desc:<.>Universe <b>rank up!</b><.>Idle production <b>+4.5%</b><.>Sparklifer power <b>+8</b><.>Unlocks the <b>Research</b> tab
		req:story7
		cost:10000000 sparks
		tag:taskStory
		class:building
		passive:hide this
		passive:multiply yield of tag:effs by 1.04
		passive:increase yield of clickEff by 7
	*TEMPLATE
		class:roundOnHover
		on tick:yield 1 upgradeCount
	*click1
		name:Click multipiler Lv.1
		desc:<.>Autoclicker production <b>+10%</b><.>Sparklifer power <b>+0.1 for each autoclicker</b>
		req:story4
		cost:50000 sparks
		tags:taskUpgrades
		passive:hide this
		passive:multiply yield of autoclickerEff by 1.1
		passive:increase yield of clickEff by autoclicker*0.1
	*click2
		name:Click multipiler Lv.2
		desc:<.>The faster you click, the stronger the sparklifer.
		req:story5, click1
		cost:200000 sparks
		tags:taskUpgrades
		passive:hide this
		passive:increase yield of clickEff by cps/250
	*autoclicker1
		name:Fast autoclicker
		desc:<.>Autoclicker production <b>+10%</b><q>"GOTTA CLICK FAST"</q>
		req:story3, 10 autoclicker
		cost:2000 sparks
		tag:taskUpgrades
		passive:hide this
		passive:multiply yield of autoclickerEff by 1.1
	*autoclicker2
		name:Faster autoclicker
		desc:<.>Autoclicker production <b>+25%</b>
		req:story3, autoclicker1
		cost:5000 sparks
		tag:taskUpgrades
		passive:hide this
		passive:multiply yield of autoclickerEff by 1.25
	*autoclicker3
		name:Even faster autoclicker
		desc:<.>Autoclicker production <b>+35%</b>
		req:story3, autoclicker2
		cost:12000 sparks
		tag:taskUpgrades
		passive:hide this
		passive:multiply yield of autoclickerEff by 1.35
	*autoclicker4
		name:The fastest autoclicker
		desc:<.>Autoclicker production <b>+45%</b>
		req:story3, autoclicker3
		cost:30000 sparks
		tag:taskUpgrades
		passive:hide this
		passive:multiply yield of autoclickerEff by 1.45
	*autoclicker5
		name:Autoclicker doubler
		desc:<.>Autoclicker production <b>+60%</b>
		req:story3, autoclicker4
		cost:90000 sparks
		tag:taskUpgrades
		passive:hide this
		passive:multiply yield of autoclickerEff by 1.6
	*sparker1
		name:Bronze tier sparkers
		desc:<.>Sparker production <b>+10%</b>
		req:story3, 15 sparker
		cost:10000 sparks
		tag:taskUpgrades
		passive:hide this
		passive:multiply yield of sparkerEff by 1.1
	*sparker2
		name:Sliver tier sparkers
		desc:<.>Sparker production <b>+20%</b>
		req:story3, sparker1
		cost:20000 sparks
		tag:taskUpgrades
		passive:hide this
		passive:multiply yield of sparkerEff by 1.2
	*sparker3
		name:Gold tier sparkers
		desc:<.>Sparker production <b>+35%</b>
		req:story3, sparker2
		cost:50000 sparks
		tag:taskUpgrades
		passive:hide this
		passive:multiply yield of sparkerEff by 1.35
	*sparker4
		name:Diamond tier sparkers
		desc:<.>Sparker production <b>+45%</b>
		req:story3, sparker3
		cost:120000 sparks
		tag:taskUpgrades
		passive:hide this
		passive:multiply yield of sparkerEff by 1.45
	*sparker5
		name:Titanium tier sparkers
		desc:<.>Sparker production <b>+60%</b>
		req:story3, sparker4
		cost:300000 sparks
		tag:taskUpgrades
		passive:hide this
		passive:multiply yield of sparkerEff by 1.6
	*spotlight1
		name:Bigger spotlights
		desc:<.>Spotlight production <b>+10%</b>
		req:story3, 20 spotlights
		cost:300000 sparks
		tag:taskUpgrades
		passive:hide this
		passive:multiply yield of spotlightEff by 1.1
	*spotlight2
		name:Bigger spotlights II
		desc:<.>Spotlight production <b>+20%</b>
		req:story3, spotlight1
		cost:700000 sparks
		tag:taskUpgrades
		passive:hide this
		passive:multiply yield of spotlightEff by 1.2
	*spotlight3
		name:Stronger spotlights 
		desc:<.>Spotlight production <b>+35%</b>
		req:story3, spotlight2
		cost:1600000 sparks
		tag:taskUpgrades
		passive:hide this
		passive:multiply yield of spotlightEff by 1.35
	*spotlight4
		name:Stronger spotlights II
		desc:<.>Spotlight production <b>+50%</b>
		req:story3, spotlight3
		cost:3500000 sparks
		tag:taskUpgrades
		passive:hide this
		passive:multiply yield of spotlightEff by 1.5
	*spotlight5
		name:Better spotlights
		desc:<.>Spotlight production <b>+70%</b>
		req:story3, spotlight4
		cost:8000000 sparks
		tag:taskUpgrades
		passive:hide this
		passive:multiply yield of spotlightEff by 1.7
	*sun1
		name:Megasuns
		desc:<.>Sun production <b>+10%</b>
		req:story3, 25 suns
		cost:680000 sparks
		tag:taskUpgrades
		passive:hide this
		passive:multiply yield of sunEff by 1.1
	*sun2
		name:Gigasuns
		desc:<.>Sun production <b>+20%</b>
		req:story3, sun1
		cost:1400000 sparks
		tag:taskUpgrades
		passive:hide this
		passive:multiply yield of sunEff by 1.2
	*sun3
		name:Terasuns
		desc:<.>Sun production <b>+35%</b>
		req:story3, sun2
		cost:3000000 sparks
		tag:taskUpgrades
		passive:hide this
		passive:multiply yield of sunEff by 1.35
	*sun4
		name:Petasuns
		desc:<.>Sun production <b>+50%</b>
		req:story3, sun3
		cost:7000000 sparks
		tag:taskUpgrades
		passive:hide this
		passive:multiply yield of sunEff by 1.5
	*sun5
		name:Exasuns
		desc:<.>Sun production <b>+70%</b>
		req:story3, sun4
		cost:16000000 sparks
		tag:taskUpgrades
		passive:hide this
		passive:multiply yield of sunEff by 1.7
	*galaxy1
		name:Tier II galaxies
		desc:<.>Galaxy production <b>+10%</b>
		req:story3, 30 galaxies
		cost:44432100 sparks
		tag:taskUpgrades
		passive:hide this
		passive:multiply yield of galaxyEff by 1.1
	*galaxy2
		name:Tier III galaxies
		desc:<.>Galaxy production <b>+20%</b>
		req:story3, galaxy1
		cost:89000000 sparks
		tag:taskUpgrades
		passive:hide this
		passive:multiply yield of galaxyEff by 1.2
	*galaxy3
		name:Tier IV galaxies
		desc:<.>Galaxy production <b>+35%</b>
		req:story3, galaxy2
		cost:200000000 sparks
		tag:taskUpgrades
		passive:hide this
		passive:multiply yield of galaxyEff by 1.35
	*galaxy4
		name:Tier V galaxies
		desc:<.>Galaxy production <b>+50%</b>
		req:story3, galaxy3
		cost:500000000 sparks
		tag:taskUpgrades
		passive:hide this
		passive:multiply yield of galaxyEff by 1.5
	*galaxy5
		name:Tier VI galaxies
		desc:<.>Galaxy production <b>+70%</b>
		req:story3, galaxy4
		cost:1200000000 sparks
		tag:taskUpgrades
		passive:hide this
		passive:multiply yield of galaxyEff by 1.7
	*doubler
		name:Spark doubler
		text:Spark doubler
		desc:Doubles idle spark production. Lasts forever.
		cost:150 antimatter
		tag:antishop permanent
		class:building
		passive:hide this
		passive:multiply yield of tag:effs by 2
	*doubler2
		name:Spark quadrupler
		text:Spark quadrupler
		desc:Quadruples idle spark production. Lasts forever.
		req:doubler
		cost:750 antimatter
		tag:antishop permanent
		class:building
		passive:hide this
		passive:multiply yield of tag:effs by 2
Shinies
	*ClickPar
		no click
		duration:1
		movement:onMouse moveTop spinRandom shrink fade
Achievements
	*TEMPLATE
		no tooltip
		class:building achievClass
		tags:achieves permanent
	*achievSpark1
		name:The Beginning
		text:<t>The Beginning</t>You gotta start somewhere...
		req:10 spark:earned
	*achievSpark2
		name:Sparkifer
		text:<t>Sparkifer</t>Make 500 sparks by any kind, all runs counted.
		req:500 spark:earned
		on tick:if (achievSpark3) hide this
	*achievSpark3
		name:Sparkifer II
		text:<t>Sparkifer II</t>Make [10000] sparks by any kind, all runs counted.
		req:10000 spark:earned
		on tick:if (achievSpark4) hide this
	*achievSpark4
		name:Sparkifer III
		text:<t>Sparkifer III</t>Make [2500000] sparks by any kind, all runs counted.
		req:2500000 spark:earned
		on tick:if (achievSpark5) hide this
	*achievSpark5
		name:Sparkifer IV
		text:<t>Sparkifer IV</t>Make [500000000] sparks by any kind, all runs counted.
		req:50000000 spark:earned
		on tick:if (achievSpark6) hide this
	*achievSpark6
		name:Sparkifer V
		text:<t>Sparkifer V</t>Make [25000000000] sparks by any kind, all runs counted.
		req:25000000000 spark:earned
		on tick:if (achievSpark7) hide this
	*achievSpark7
		name:Sparkifer VI
		text:<t>Sparkifer VI</t>Make [5000000000000] sparks by any kind, all runs counted.
		req:5000000000000 spark:earned
		on tick:if (achievSpark8) hide this
	*achievSpark8
		name:Sparkifer VII
		text:<t>Sparkifer VII</t>Make [100000000000000] sparks by any kind, all runs counted.
		req:100000000000000 spark:earned
		on tick:if (achievSpark9) hide this
	*achievSpark9
		name:Sparkifer VIII
		text:<t>Sparkifer VIII</t>Make [50000000000000000] sparks by any kind, all runs counted.
		req:50000000000000000 spark:earned
		on tick:if (achievSpark10) hide this
	*achievSpark10
		name:Sparkifer IX
		text:<t>Sparkifer IX</t>Make [2500000000000000000] sparks by any kind, all runs counted.
		req:2500000000000000000 spark:earned
		on tick:if (achievSpark11) hide this
	*achievSpark11
		name:Sparkifer X
		text:<t>Sparkifer X</t>Make [500000000000000000000] sparks by any kind, all runs counted.
		req:500000000000000000000 spark:earned
	*achievClick1
		name:Addicted clicker
		text:<t>Addicted clicker</t>Click the sparklifer 100 times.
		on tick:if (achievClick2) hide this
		req:100 sparklifer:clicks
	*achievClick2
		name:Addicted clicker II
		text:<t>Addicted clicker II</t>Click the sparklifer 500 times.
		on tick:if (achievClick3) hide this
		req:500 sparklifer:clicks
	*achievClick3
		name:Addicted clicker III
		text:<t>Addicted clicker III</t>Click the sparklifer [2500] times.
		on tick:if (achievClick4) hide this
		req:2500 sparklifer:clicks
	*achievClick4
		name:Addicted clicker IV
		text:<t>Addicted clicker IV</t>Click the sparklifer [10000] times.
		on tick:if (achievClick5) hide this
		req:10000 sparklifer:clicks
	*achievClick5
		name:Addicted clicker V
		text:<t>Addicted clicker V</t>Click the sparklifer [30000] times.
		on tick:if (achievClick6) hide this
		req:30000 sparklifer:clicks
	*achievClick6
		name:Addicted clicker VI
		text:<t>Addicted clicker VI</t>Click the sparklifer [250000] times.
		on tick:if (achievClick7) hide this
		req:250000 sparklifer:clicks
	*achievClick7
		name:Addicted clicker VII
		text:<t>Addicted clicker VII</t>Click the sparklifer [1000000] times.
		on tick:if (achievClick8) hide this
		req:1000000 sparklifer:clicks
	*achievClick8
		name:Addicted clicker VIII
		text:<t>Addicted clicker VIII</t>Click the sparklifer [5000000] times.
		on tick:if (achievClick9) hide this
		req:5000000 sparklifer:clicks
	*achievClick9
		name:Addicted clicker IX
		text:<t>Addicted clicker IX</t>Click the sparklifer [25000000] times.
		on tick:if (achievClick10) hide this
		req:25000000 sparklifer:clicks
	*achievClick10
		name:Addicted clicker X
		text:<t>Addicted clicker X</t>Click the sparklifer [100000000] times.
		req:100000000 sparklifer:clicks
	*achievIdleProd1
		name:Mediocre producer
		text:<t>Mediocre producer</t>Reach 10 base spark per second
		on tick:if (achievIdleProd2) hide this
		req:10 spark:ps
	*achievIdleProd2
		name:Mediocre producer II
		text:<t>Mediocre producer II</t>Reach 50 base spark per second
		on tick:if (achievIdleProd3) hide this
		req:50 spark:ps
	*achievIdleProd3
		name:Mediocre producer III
		text:<t>Mediocre producer III</t>Reach 250 base spark per second
		on tick:if (achievIdleProd4) hide this
		req:250 spark:ps
	*achievIdleProd4
		name:Mediocre producer IV
		text:<t>Mediocre producer IV</t>Reach [1000] base spark per second
		on tick:if (achievIdleProd5) hide this
		req:1000 spark:ps
	*achievIdleProd5
		name:Mediocre producer V
		text:<t>Mediocre producer V</t>Reach [5000] base spark per second
		on tick:if (achievIdleProd6) hide this
		req:5000 spark:ps
	*achievIdleProd6
		name:Productive producer
		text:<t>Productive producer</t>Reach [25000] base spark per second
		on tick:if (achievIdleProd7) hide this
		req:25000 spark:ps
	*achievIdleProd7
		name:Productive producer II
		text:<t>Productive producer</t>Reach [100000] base spark per second
		on tick:if (achievIdleProd8) hide this
		req:100000 spark:ps
	*achievIdleProd8
		name:Productive producer III
		text:<t>Productive producer</t>Reach [500000] base spark per second
		on tick:if (achievIdleProd9) hide this
		req:500000 spark:ps
	*achievIdleProd9
		name:Productive producer IV
		text:<t>Productive producer</t>Reach [2500000] base spark per second
		on tick:if (achievIdleProd10) hide this
		req:2500000 spark:ps
	*achievIdleProd10
		name:Productive producer V
		text:<t>Productive producer</t>Reach [10000000] base spark per second
		on tick:if (achievIdleProd11) hide this
		req:10000000 spark:ps
	*achievIdleProd11
		name:Godlike producer
		text:<t>Godlike producer</t>Reach [50000000] base spark per second
		on tick:if (achievIdleProd12) hide this
		req:50000000 spark:ps
	*achievIdleProd12
		name:Godlike producer II
		text:<t>Godlike producer II</t>Reach [250000000] base spark per second
		on tick:if (achievIdleProd13) hide this
		req:250000000 spark:ps
	*achievIdleProd13
		name:Godlike producer III
		text:<t>Godlike producer III</t>Reach [1000000000] base spark per second
		on tick:if (achievIdleProd14) hide this
		req:1000000000 spark:ps
	*achievIdleProd14
		name:Godlike producer IV
		text:<t>Godlike producer IV</t>Reach [5000000000] base spark per second
		on tick:if (achievIdleProd15) hide this
		req:5000000000 spark:ps
	*achievIdleProd15
		name:Godlike producer V
		text:<t>Godlike producer V</t>Reach [25000000000] base spark per second
		req:25000000000 spark:ps
	*achievPrestige1
		name:Born again
		text:<t>Born again</t>Ascend your universe once.
		on tick:if (achievPrestige2) hide this
		req:1 prestigeTimes
	*achievPrestige2
		name:Born again II
		text:<t>Born again II</t>Ascend your universe twice.
		on tick:if (achievPrestige3) hide this
		req:2 prestigeTimes
	*achievPrestige3
		name:Born again III
		text:<t>Born again III</t>Ascend your universe 5 times.
		on tick:if (achievPrestige4) hide this
		req:5 prestigeTimes
	*achievPrestige4
		name:Born again IV
		text:<t>Born again IV</t>Ascend your universe 10 times.
		on tick:if (achievPrestige5) hide this
		req:10 prestigeTimes
	*achievPrestige5
		name:Born again V
		text:<t>Born again V</t>Ascend your universe 20 times.
		req:20 prestigeTimes
	*achievPlaytime1
		name:Timewaster
		text:<t>Timewaster</t>Play the game for 10 minutes.
		on tick:if (achievPlaytime2) hide this
		req:600 playtime
	*achievPlaytime2
		name:Timewaster II
		text:<t>Timewaster II</t>Play the game for 30 minutes.
		on tick:if (achievPlaytime3) hide this
		req:1800 playtime
	*achievPlaytime3
		name:Timewaster III
		text:<t>Timewaster III</t>Play the game for 1 hour.
		on tick:if (achievPlaytime4) hide this
		req:3600 playtime
	*achievPlaytime4
		name:Timewaster IV
		text:<t>Timewaster IV</t>Play the game for 2 hours.
		on tick:if (achievPlaytime5) hide this
		req:7200 playtime
	*achievPlaytime5
		name:Timewaster V
		text:<t>Timewaster V</t>Play the game for 4 hours.
		on tick:if (achievPlaytime6) hide this
		req:14400 playtime
	*achievPlaytime6
		name:Timewaster VI
		text:<t>Timewaster VI</t>Play the game for 12 hours.
		on tick:if (achievPlaytime7) hide this
		req:43200 playtime
	*achievPlaytime7
		name:Timewaster VII
		text:<t>Timewaster VII</t>Play the game for 1 day.
		on tick:if (achievPlaytime8) hide this
		req:86400 playtime
	*achievPlaytime8
		name:Timewaster VIII
		text:<t>Timewaster VIII</t>Play the game for 2 days.
		on tick:if (achievPlaytime9) hide this
		req:172800 playtime
	*achievPlaytime9
		name:Timewaster IX
		text:<t>Timewaster IX</t>Play the game for 5 days.
		on tick:if (achievPlaytime10) hide this
		req:432000 playtime
	*achievPlaytime10
		name:Timewaster X
		text:<t>Timewaster X</t>Play the game for 10 days.
		req:864000 playtime
Items
	*TEMPLATE
		tags:artifacts permanent
		shown
		class:building
	*clickScroll1
		name:The Scroll of Clicking Techniques
		text:<t>The Scroll of Clicking Techniques</t><b>Normal</b><//>Upgrade for <b>5 fragments</b>
		desc:<//>Uniqueness: <b>Normal</b><//>Sparklifer power +0.5%
		passive:multiply yield of clickEff by 1.005
		on click:
			if (fragment>=5)
				yield clickScroll2
				lose this
			end
		end
	*clickScroll2
		name:The Scroll of Clicking Techniques
		text:<t>The Scroll of Clicking Techniques</t><#3a3><b>Complexed</b></#><//>Upgrade for <b>20 fragments</b>
		desc:<//>Uniqueness: <#0a0><b>Complexed</b></#><//>Sparklifer power +1%
		passive:multiply yield of clickEff by 1.01
		on click:
			if (fragment>=20)
				yield clickScroll3
				lose this
			end
		end
	*clickScroll3
		name:The Scroll of Clicking Techniques
		text:<t>The Scroll of Clicking Techniques</t><#33a><b>Ultra-complexed</b></#><//>Upgrade for <b>100 fragments</b>
		desc:<//>Uniqueness: <#33a><b>Ultra-complexed</b></#><//>Sparklifer power +2%
		passive:multiply yield of clickEff by 1.02
		on click:
			if (fragment>=100)
				yield clickScroll4
				lose this
			end
		end
	*clickScroll4
		name:The Scroll of Clicking Techniques
		text:<t>The Scroll of Clicking Techniques</t><#a3a><b>Rare</b></#><//>Upgrade for <b>400 fragments</b>
		desc:<//>Uniqueness: <#a3a><b>Rare</b></#><//>Sparklifer power +4%
		passive:multiply yield of clickEff by 1.04
		on click:
			if (fragment>=400)
				yield clickScroll5
				lose this
			end
		end
	*clickScroll5
		name:The Scroll of Clicking Techniques
		text:<t>The Scroll of Clicking Techniques</t><#aa3><b>Epic</b></#><//>Upgrade for <b>[1600] fragments</b>
		desc:<//>Uniqueness: <#aa3><b>Epic</b></#><//>Sparklifer power +8%
		passive:multiply yield of clickEff by 1.08
		on click:
			if (fragment>=1600)
				yield clickScroll6
				lose this
			end
		end
	*clickScroll6
		name:The Scroll of Clicking Techniques
		text:<t>The Scroll of Clicking Techniques</t><#a73><b>Special</b></#><//>Upgrade for <b>[6400] fragments</b>
		desc:<//>Uniqueness: <#a73><b>Special</b></#><//>Sparklifer power +16%
		passive:multiply yield of clickEff by 1.16
		on click:
			if (fragment>=6400)
				yield clickScroll7
				lose this
			end
		end
	*clickScroll7
		name:The Scroll of Clicking Techniques
		text:<t>The Scroll of Clicking Techniques</t><#a33><b>Legendary</b></#><//>Upgrade for <b>[25600] fragments</b>
		desc:<//>Uniqueness: <#a33><b>Legendary</b></#><//>Sparklifer power +32%
		passive:multiply yield of clickEff by 1.32
		on click:
			if (fragment>=25600)
				yield clickScroll8
				lose this
			end
		end
	*clickScroll8
		name:The Scroll of Clicking Techniques
		text:<t>The Scroll of Clicking Techniques</t><#a00><b>Mythic</b></#><//><b>Max reached!</b>
		desc:<//>Uniqueness: <#a00><b>Mythic</b></#><//>Sparklifer power +64%
		passive:multiply yield of clickEff by 1.64